import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.android.application'
}

tasks.register('configure_gstreamer_java', Copy) {
    from "${System.env.GSTREAMER_1_0_ROOT_ANDROID}/x86/share/gst-android/ndk-build/"
    include 'GStreamer.java'
    include 'androidmedia/GstAhcCallback.java'
    include 'androidmedia/GstAhsCallback.java'
    include 'androidmedia/GstAmcOnFrameAvailableListener.java'
    into layout.buildDirectory.dir('from-gst-android/org/freedesktop/gstreamer')
    filter(ReplaceTokens, tokens: [INCLUDE_CA_CERTIFICATES: '//', INCLUDE_FONTS: '//', INCLUDE_COPY_FILE: '//'])
}
preBuild.dependsOn configure_gstreamer_java

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {

    // Determines the version of Android SDK Platform
    compileSdk 34
    defaultConfig {
        applicationId 'com.s2e_systems.publisher'
        targetSdk 33
        minSdk 28
        versionCode 6
        versionName "1.0"
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            buildStagingDirectory = new File(rootProject.projectDir, ".build-ndk/" + project.name)
        }
    }
    buildFeatures {
        viewBinding = true
    }
    sourceSets.main.java.srcDirs += layout.buildDirectory.dir('from-gst-android')
    namespace 'com.s2e_systems.publisher'
    buildDir = new File(rootProject.projectDir, ".build-gradle/" + project.name)
}
dependencies {
    implementation 'com.google.android.material:material:1.11.0'
}
